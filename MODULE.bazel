"""Operator build and test dependencies."""

# NOTE: When editing this file, also update the lockfile.
#   bazel mod deps --lockfile_mode=update

module(
    name = "operator",
    repo_name = "io_operator",
)

# =========================================
# Operator module dependencies
# =========================================

bazel_dep(name = "aspect_bazel_lib", version = "2.7.0")
bazel_dep(name = "rules_jvm_external", version = "6.0")
bazel_dep(name = "contrib_rules_jvm", version = "0.26.0")
bazel_dep(name = "rules_oci", version = "1.7.6")

# =========================================
# Java dependencies
# =========================================

FABRIC8_K8S_CLIENT_VERSION = "6.11.0"
FLOGGER_VERSION = "0.8"
GUICE_VERSION = "5.1.0"
JETTY_VERSION = "11.0.15"
JOSDK_VERSION = "4.8.3"
LOG4J_VERSION = "2.23.0"

maven = use_extension("@rules_jvm_external//:extensions.bzl", "maven")
maven.install(
    artifacts = [
        # keep sorted
        "com.google.flogger:flogger:%s" % FLOGGER_VERSION,
        "com.google.flogger:flogger-log4j2-backend:%s" % FLOGGER_VERSION,
        "com.google.inject.extensions:guice-assistedinject:%s" % GUICE_VERSION,
        "com.google.inject:guice:%s" % GUICE_VERSION,
        "io.fabric8:crd-generator-apt:%s" % FABRIC8_K8S_CLIENT_VERSION,
        "io.fabric8:istio-client:%s" % FABRIC8_K8S_CLIENT_VERSION,
        "io.fabric8:java-generator-cli:%s" % FABRIC8_K8S_CLIENT_VERSION,
        "io.fabric8:kubernetes-client:%s" % FABRIC8_K8S_CLIENT_VERSION,
        "io.javaoperatorsdk:micrometer-support:%s" % JOSDK_VERSION,
        "io.javaoperatorsdk:operator-framework:%s" % JOSDK_VERSION,
        "org.apache.logging.log4j:log4j-api:%s" % LOG4J_VERSION,
        "org.apache.logging.log4j:log4j-core:%s" % LOG4J_VERSION,
        "org.apache.logging.log4j:log4j-slf4j-impl:%s" % LOG4J_VERSION,
        "org.bouncycastle:bcpkix-jdk18on:1.73",
        "org.eclipse.jetty:jetty-server:%s" % JETTY_VERSION,
        "org.eclipse.jetty:jetty-servlet:%s" % JETTY_VERSION,
        "org.eclipse.jgit:org.eclipse.jgit:6.5.0.202303070854-r",
        "org.projectlombok:lombok:1.18.32",
    ],
    # Don't forget to change this back to True before submitting your change.
    fail_if_repin_required = True,
    lock_file = "//:maven_install.json",
    repositories = [
        "https://repo1.maven.org/maven2",
    ],
    resolve_timeout = 1200,
)

JUNIT_JUPITER_VERSION = "5.10.2"

JUNIT_PLATFORM_VERSION = "1.10.2"

# Test only maven dependencies
[
    maven.artifact(
        testonly = True,
        artifact = artifact,
        group = group,
        version = version,
    )
    for group, artifact, version in [coord.split(":") for coord in [
        "com.squareup.okhttp3:mockwebserver:4.12.0",
        "com.urswolfer.gerrit.client.rest:gerrit-rest-java-client:0.9.5",
        "io.fabric8:kubernetes-server-mock:%s" % FABRIC8_K8S_CLIENT_VERSION,
        "io.fabric8:mockwebserver:%s" % FABRIC8_K8S_CLIENT_VERSION,
        "io.javaoperatorsdk:operator-framework-junit-5:%s" % JOSDK_VERSION,
        "junit:junit:4.13.2",
        "org.junit.platform:junit-platform-launcher:%s" % JUNIT_PLATFORM_VERSION,
        "org.junit.platform:junit-platform-reporting:%s" % JUNIT_PLATFORM_VERSION,
        "org.junit.jupiter:junit-jupiter-api:%s" % JUNIT_JUPITER_VERSION,
        "org.junit.jupiter:junit-jupiter-params:%s" % JUNIT_JUPITER_VERSION,
        "org.junit.jupiter:junit-jupiter-engine:%s" % JUNIT_JUPITER_VERSION,
        "org.mockito:mockito-core:4.3.1",
        "org.hamcrest:hamcrest-core:1.3",
        "com.google.truth:truth:1.1.2",
    ]]
]

use_repo(maven, "maven", "unpinned_maven")

DISTROLESS_JAVA_DIGEST = "sha256:9c42eb0c1a00cfe17ddffd6eb4a4ae625f065add2a890bfc8aa98cbd8730c3ce"

oci = use_extension("@rules_oci//oci:extensions.bzl", "oci")
oci.pull(
    name = "distroless_java",
    digest = DISTROLESS_JAVA_DIGEST,
    image = "gcr.io/distroless/java17",
)
use_repo(oci, "distroless_java")
