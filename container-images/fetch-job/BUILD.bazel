load("@aspect_bazel_lib//lib:yq.bzl", "yq")
load("@rules_apko//apko:defs.bzl", "apko_image", "apko_lock")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_push", "oci_tarball")
load("@rules_pkg//pkg:tar.bzl", "pkg_tar")

yq(
    name = "merge-base",
    srcs = [
        "apko.part.yaml",
        "//container-images/base:apko.part.yaml",
    ],
    outs = ["apko.yaml"],
    expression = ". as $item ireduce ({}; . *+ $item )",
)

apko_lock(
    name = "lock",
    config = ":merge-base",
    lockfile_name = "apko.lock.json",
)

apko_image(
    name = "apko-fetch-job",
    architecture = select({
        "@platforms//cpu:arm64": "arm64",
        "@platforms//cpu:x86_64": "amd64",
    }),
    config = "apko.yaml",
    contents = "@fetch-job_lock//:contents",
    tag = "fetch-job:latest",
)

pkg_tar(
    name = "fetch-job_layer",
    srcs = ["tools/fetch-job.sh"],
    package_dir = "/var/tools",
)

oci_image(
    name = "fetch-job",
    base = ":apko-fetch-job",
    entrypoint = [
        "/var/tools/fetch-job.sh",
    ],
    tars = [
        ":fetch-job_layer",
    ],
)

oci_tarball(
    name = "build",
    image = ":fetch-job",
    repo_tags = ["fetch-job:latest"],
    visibility = ["//visibility:public"],
)

oci_push(
    name = "publish",
    image = ":fetch-job",
    repository = "docker.io/k8sgerrit/fetch-job",
    remote_tags = "//:full-version",
    visibility = ["//visibility:public"],
)
