load("@aspect_bazel_lib//lib:tar.bzl", "tar")
load("@container_structure_test//:defs.bzl", "container_structure_test")
load("@rules_jvm_external//:defs.bzl", "artifact")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_tarball")
load("//tools/bzl:genrule2.bzl", "genrule2")

java_binary(
    name = "java-generator-cli",
    main_class = "io.fabric8.java.generator.cli.GenerateJavaSources",
    visibility = ["//visibility:public"],
    runtime_deps = [
        artifact("io.fabric8:java-generator-cli"),
    ],
)

java_binary(
    name = "delombok-bin",
    main_class = "lombok.launch.Main",
    runtime_deps = [
        artifact("org.projectlombok:lombok"),
        "@maven//:io_fabric8_generator_annotations",
        "@maven//:io_fabric8_kubernetes_client_api",
        "@maven//:io_sundr_builder_annotations",
    ],
)

genrule2(
    name = "generate-ambassador-bindings",
    srcs = ["src/main/resources/crd/emissary-crds.yaml"],
    outs = ["generate-ambassador-bindings.zip"],
    cmd = " && ".join([
        "$(location :java-generator-cli) --target $$TMP --add-extra-annotations --source $<",
        "cd $$TMP",
        "zip -q $$ROOT/$@ $$(find . -type f)",
    ]),
    tools = [":java-generator-cli"],
)

genrule2(
    name = "delombok-ambassador-bindings",
    srcs = [":generate-ambassador-bindings"],
    outs = ["delombok-ambassador-bindings.srcjar"],
    cmd = " && ".join([
        "unzip -q -d $$TMP $<",
        "$(location :delombok-bin) delombok --target $$TMP/delombok $$TMP",
        "cd $$TMP/delombok",
        "zip -q $$ROOT/$@ $$(find . -type f)",
    ]),
    tools = [":delombok-bin"],
)

java_plugin(
    name = "crd-generator-plugin",
    processor_class = "io.fabric8.crd.generator.apt.CustomResourceAnnotationProcessor",
    deps = [
        "@maven//:io_fabric8_crd_generator_apt",
    ],
)

java_plugin(
    name = "sundr-buildable-processor-plugin",
    processor_class = "io.sundr.builder.internal.processor.BuildableProcessor",
    deps = [
        "@maven//:io_sundr_builder_annotations",
    ],
)

java_plugin(
    name = "sundr-external-buildable-processor-plugin",
    processor_class = "io.sundr.builder.internal.processor.ExternalBuildableProcessor",
    deps = [
        "@maven//:io_sundr_builder_annotations",
    ],
)

java_library(
    name = "sundr-external-buildable-processor",
    exported_plugins = [
        ":sundr-buildable-processor-plugin",
        ":sundr-external-buildable-processor-plugin",
    ],
    exports = ["@maven//:io_sundr_builder_annotations"],
)

java_library(
    name = "ambassador-bindings-lib",
    srcs = [":delombok-ambassador-bindings"],
    deps = [
        ":sundr-external-buildable-processor",
        "@maven//:com_fasterxml_jackson_core_jackson_annotations",
        "@maven//:com_fasterxml_jackson_core_jackson_databind",
        "@maven//:io_fabric8_generator_annotations",
        "@maven//:io_fabric8_kubernetes_client_api",
        "@maven//:io_fabric8_kubernetes_model_common",
        "@maven//:io_fabric8_kubernetes_model_core",
    ],
)

# TODO(davido): The package is generated in Bazel build: io.fabric8.kubernetes.api.
# This is conflicting with the version of the library on the classpath.
# Remove this generated package from the final artifact for now and investigate later
# why this is happening in the first place.
genrule2(
    name = "stripped-ambassador-bindings",
    srcs = ["ambassador-bindings-lib"],
    outs = ["stripped-ambassador-bindings.jar"],
    cmd = " && ".join([
        "unzip -q -d $$TMP $<",
        "cd $$TMP",
        "rm -rf io/fabric8",
        "zip -qr $$ROOT/$@ .",
    ]),
)

java_import(
    name = "stripped-ambassador-bindings-impl",
    jars = [":stripped-ambassador-bindings"],
)

java_library(
    name = "crd-generator-processor",
    exported_plugins = [
        ":crd-generator-plugin",
    ],
    exports = ["@maven//:io_fabric8_crd_generator_apt"],
)

java_library(
    name = "operator",
    srcs = glob(["src/main/java/**/*.java"]),
    resources = glob(["src/main/resources/**"]),
    deps = [
        ":crd-generator-processor",
        ":stripped-ambassador-bindings-impl",
        "@maven//:com_fasterxml_jackson_core_jackson_annotations",
        "@maven//:com_fasterxml_jackson_core_jackson_core",
        "@maven//:com_fasterxml_jackson_core_jackson_databind",
        "@maven//:com_fasterxml_jackson_dataformat_jackson_dataformat_yaml",
        "@maven//:com_google_flogger_flogger",
        "@maven//:com_google_flogger_flogger_log4j2_backend",
        "@maven//:com_google_guava_guava",
        "@maven//:com_google_inject_extensions_guice_assistedinject",
        "@maven//:com_google_inject_guice",
        "@maven//:io_fabric8_crd_generator_apt",
        "@maven//:io_fabric8_generator_annotations",
        "@maven//:io_fabric8_istio_client",
        "@maven//:io_fabric8_istio_model_v1beta1",
        "@maven//:io_fabric8_kubernetes_client",
        "@maven//:io_fabric8_kubernetes_client_api",
        "@maven//:io_fabric8_kubernetes_model_admissionregistration",
        "@maven//:io_fabric8_kubernetes_model_apps",
        "@maven//:io_fabric8_kubernetes_model_batch",
        "@maven//:io_fabric8_kubernetes_model_common",
        "@maven//:io_fabric8_kubernetes_model_core",
        "@maven//:io_fabric8_kubernetes_model_networking",
        "@maven//:io_fabric8_kubernetes_model_node",
        "@maven//:io_javaoperatorsdk_micrometer_support",
        "@maven//:io_javaoperatorsdk_operator_framework",
        "@maven//:io_javaoperatorsdk_operator_framework_core",
        "@maven//:io_sundr_builder_annotations",
        "@maven//:org_apache_commons_commons_lang3",
        "@maven//:org_apache_logging_log4j_log4j_api",
        "@maven//:org_apache_logging_log4j_log4j_core",
        "@maven//:org_apache_logging_log4j_log4j_slf4j_impl",
        "@maven//:org_bouncycastle_bcpkix_jdk18on",
        "@maven//:org_bouncycastle_bcprov_jdk18on",
        "@maven//:org_eclipse_jetty_jetty_server",
        "@maven//:org_eclipse_jetty_jetty_servlet",
        "@maven//:org_eclipse_jetty_jetty_util",
        "@maven//:org_eclipse_jetty_toolchain_jetty_jakarta_servlet_api",
        "@maven//:org_eclipse_jgit_org_eclipse_jgit",
    ],
)

# TODO(davido): Wait until the packages are refactored and separated: operator packages should
# depend on crd package: com.google.gerrit.k8s.operator.api.model, but not the other way around.
# However, currently we have a dependency cycle.
#java_library(
#    name = "crd-resources",
#    srcs = glob(["src/main/java/com/google/gerrit/k8s/operator/api/model/**/*.java"]),
#    deps = [
#        ":crd-generator-processor",
#    ],
#)

java_binary(
    name = "operator-binary",
    main_class = "com.google.gerrit.k8s.operator.Main",
    runtime_deps = [":operator"],
)

exports_files(
    ["maven_install.json"],
    visibility = ["//visibility:public"],
)

tar(
    name = "layer",
    srcs = ["operator-binary_deploy.jar"],
)

oci_image(
    name = "k8s-gerrit-operator-image",
    base = "@distroless_java",
    entrypoint = [
        "java",
        "-jar",
        "/operator-binary_deploy.jar",
    ],
    tars = [":layer"],
)

oci_tarball(
    name = "k8s-gerrit-operator-image-tarball",
    image = ":k8s-gerrit-operator-image",
    repo_tags = ["k8s-gerrit-operator-image:latest"],
)

# TODO(davido): Currently failing, because iamge is expecting environment variables to be set
container_structure_test(
    name = "container_test",
    configs = ["container-structure-test.yaml"],
    image = ":image",
    tags = ["requires-docker"],
)
